This year, Newspaper is 3rd period, right before lunch. All morning, you've been
looking forward to this class and catching up with Ms. Grainger. As you walk
into the room, you can't help but smile as memories from last year come flooding
back.

Ms. Grainger catches your eye and smiles at you. Just as she's starting to wave
you over, someone steps in front of you, blocking your line of sight: Mercedes
Grant, flanked by her cronies, Piper and Peyton.

"Oh," she says, lowering her eyelids, "You're in this class?" She sighs
dramatically and Piper smirks.

*choice
    #Ask how she missed your publications.
        You narrow your eyes at her. "Uh… yeah, Mercedes. I had, like, six
        prominent articles published last year. Did you take this class without
        even having read the newspaper before?"

        Mercedes shrugs, still elaborately bored. "I know about newspapers. I
        guess my dad owns the real one."

        The "real" newspaper is the Ennisburg Herald and this is news to you. Of
        course, Mercedes's parents own a lot of things, so it doesn't seem
        implausible.

        *choice
            #Tell her that that's not the same as knowing about newspapers.
                You narrow your eyes at her, incredulous. "That's… not the same
                as knowing about newspapers, Mercedes. It actually takes work to
                put one together, which is a concept I'm sure you're completely
                unfamiliar with."

                She rolls her eyes at you. "Pff. What[i]ever[/i], troll." She
                and her friends turn as one and sit down.

                *page_break
                *goto after_mercedes

            #Just be mean.
                Why did you ask her a question, anyway? It's not like you could
                have expected her to have something worthwhile to say.

                *goto trash_talk

            #Say nothing. Walk around her.
                *goto walk_past

    #Ask why she's in this class.
        "Yeah, I am, Mercedes," you say, "Why are you in this class?" Her
        presence here seems bizarre, since she wasn't in this class last year.

        "It seems like an easy A," she says, "and it'll look good on college
        applications. I don't need to justify myself to you, though. Get out of
        my face, loser."

        Wait. Wasn't she the one who came to talk to you? In any case, she and
        her friends turn to sit down.

        *page_break
        *goto after_mercedes

    #Just be mean.
        *label trash_talk

        You pretend to have just noticed her and in your most polite voice, you
        say, "Oh!  Mercedes. Your face hole is leaking noises again. You should
        probably see to that."

        Mercedes rolls her eyes at you. "Why do I even try with you? You
        obviously need more help than ten of me could give." She looks your
        wardrobe up and down disdainfully.

        *choice
            #Call her on her non sequitur.
                "Great, Mercedes," you say, "Way to stay on topic with your
                insults."

                She rolls her eyes at you. Again. "Pff. What[i]ever[/i], troll."
                She and her friends turn as one and sit down.

                Despite the fact that it looked to you like you got the better
                of that exchange, everyone who overheard seems to think Mercedes
                came out on top. You just can't win against the popular crowd.

                *page_break
                *goto after_mercedes

            #Tell her to shut up.
                "Shut up, Mercedes," you say, "Get out of my way."

                *goto walk_past

            #Say something that'll cut deep.
                "Aw," you say, pretending to be concerned, "Are you in a bad
                mood today, Mercedes? Was mummy drunk and mean at breakfast
                again?"

                Mercedes blinks at you, startled. "Die in a fire," she says
                viciously, then turns on her heel with a sniff to find a seat.
                Peyton gives you a dirty look, then she and Piper sit, as well.

                *page_break
                *goto after_mercedes

            #Say nothing. Walk around her.
                *goto walk_past

    #Say nothing. Walk around her.
        *label walk_past

        *temp hair_insult "ridiculous"
        *if (hair = "short") or ((hair = "long") or (hair = "chin-length"))
            *set hair_insult "boring"

        You shake your head wordlessly at her and shoulder past her. As you do,
        you catch her murmur something to Piper and Peyton about your
        "${hair_insult} ${hair} hair".

        *page_break
        *goto after_mercedes


*label after_mercedes

With Mercedes out of your face, you look toward the front of the room, hoping to
have time to catch up with Ms. Grainger, but before you can take a step, the
bell rings, signalling the start of class. You quickly find a seat.

A lot of the class time is spent on orienting the new folks to being on the
newspaper staff, how the class works and what the work will consist of. During
all of this you…

*fake_choice
    #zone out and think about what you'll do after school.
        Maybe you and Kaitlin can head down to Baker Street to hang out. Or go
        mess around at Bailey Park.

    #zone out and worry about the $!{ex_antonia} situation.
        What are you going to do about $!{ex_antonia}? At least you haven't seen
        ${ex_her} yet. And seeing Mercedes was… survivable.

    #zone out and think about what your first story should be.
        You start brainstorming story ideas, but quickly get side tracked
        fantasizing about a hit-piece editorial on the topic of all the ways in
        which Mercedes is vile.

    #listen raptly to every word.
        Even though it's all stuff you've heard before, you listen attentively
        to every word Ms. Grainger says. Newspaper is, after all, your favorite
        class. And, anyway, it sets a good example for the newer students.


Eventually, Ms. Grainger starts handing out assignments about who will work on
what kinds of tasks. This year, she assigns you a full-time reporter position.
Mercedes, Piper and Peyton all get assigned as full-time photographers, which
seems suspicious until Ms. Grainger makes an announcement.

"I have some exciting news," Ms. Grainger says, "This year, we've received a
very generous equipment donation from the Herald." She hefts an expensive
looking camera from behind her desk and shows it to the class. "Mr. Grant
donated a handful of very nice medium format film cameras to us. I'll be issuing
five of them shortly."

"I'll call the names of the people that will get to try out the new cameras.
Meanwhile, everyone get acquainted with the computer system. Returning staffers,
help out our new folks."

You cluster around a computer with some other students and start getting the new
ones acquainted with the submission, editing and publishing system. You keep
your ear out for your name, but it doesn't get called. In fact, you notice that
of the five cameras being issued, three are going to Mercedes and her crew. How
do you feel about that?

*fake_choice
    #Typical. Those kids always get everything they want.
        If Mercedes's dad wanted to buy her a nice camera, he could just have
        done so. Why do they have to pretend that it's for everyone, if they're
        just going to hand it back to her and her friends anyway?

    #It makes sense. They did get assigned photographer positions, after all.
        If Mercedes's dad wanted to buy her a nice camera, he could just have
        done so. This way, at least the school can keep the cameras and some
        other students get experience working with nice equipment.

    #Meh. Ms. Grainger can do what she wants. It's her equipment.
        You'll make do with what you have, and get the job done. It's what you
        did last year and it was great. You don't need a fancy camera to do what
        you need to.


As the bell rings, ending class, Ms. Grainger calls out, "Tomorrow we'll start
working on story ideas and make assignments! Victoria, can I talk to you a
minute?"

You wait while the rest of the staff files out of the room. Ms. Grainger sits
down at her desk and shuffles some papers. Once everyone else is gone, she stops
and says, "Hi, Victoria. I hope you had a good summer?"

*fake_choice
    #Change the subject.
        "What'd you want to see me about, Ms. Grainger?" you say, glancing down.

        She pauses a beat, slightly surprised, then says, "That good, huh? Well,
        hopefully this will make you smile."

    #Be honest, but guarded.
        "It…" you say, "It could have been better. But I'm excited about this
        year. What'd you want to see me about?"

        "Well," Ms. Grainger says, "I'm glad you're looking forward to this
        year. I have something for you that I hope portends good things for
        you."

    #Fill her in.
        "It was awful," you say, "I don't know if you heard, but $!{ex_antonia}
        and I… well, ${ex_she} dumped me at the end of the year. And ${ex_she}'s
        dating Mercedes. They seem really happy together and they're probably
        not trying to rub it in my face, but it mostly feels that way."

        "Ah," Ms. Grainger says, "That would explain the exchange before class."
        She looks thoughtful a moment. "Well, I have something for you. It won't
        solve your ${ex_girl}friend problems, but it might cheer you up."

    #[i](Lie)[/i] Tell her it was great.
        "It was great, Ms. Grainger," you say, "But I'm excited to get back to
        work on the newspaper."

        "I'm glad to hear that, Victoria," she says.


She reaches down behind her desk and reveals a sixth new camera with a smile.
"After the hard work you put in last year and the promise you show, I wanted to
try something new out. I'm going to assign you to some stories solo for a while.
You'll be in charge of taking your own photos, as well as writing. If it works
well and you like it, we'll keep doing it and maybe expand it to some other
students. If it doesn't work out," she just shrugs.

She sets the camera in the bag that came with it and hands it to you. "The user
manual is in there. You should read it. And I know you know this, but don't lose
or break this, please. They're really, very expensive."

Just then, the warning bell rings. You have two minutes to get to your next
class. Looping the camera bag's strap over your head, you hurry for the door and
call out over your shoulder with a smile and a wave, "Thanks, Ms. Grainger!"

*page_break A few days later…
*goto_scene purse_snatch
